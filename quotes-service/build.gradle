plugins {
  id 'org.springframework.boot' version '2.7.0'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'idea'
  //id 'org.springframework.experimental.aot' version '0.10.5'
  id 'net.researchgate.release' version '2.8.1'
  id 'com.google.protobuf' version '0.8.18'
  id 'com.gorylenko.gradle-git-properties' version '2.4.1'
}

group = 'io.kong.developer'
sourceCompatibility = '11'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  maven { url 'https://repo.spring.io/snapshot' }
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation "net.datafaker:datafaker:1.4.0"
  implementation 'io.dekorate:kubernetes-spring-starter:2.9.3'
  annotationProcessor 'io.dekorate:kubernetes-annotations:3.0.0'

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  //grpc stuff
  implementation 'net.devh:grpc-server-spring-boot-starter:2.13.1.RELEASE'
  implementation 'com.google.protobuf:protobuf-java:3.21.1'
  implementation 'io.grpc:grpc-stub:1.47.0'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.projectreactor:reactor-test'
}

test {
  useJUnitPlatform()
}

release {
  git {
    requireBranch = 'main'
  }
}

protobuf {

  //generatedFilesBaseDir = "$buildDir/generated-main-proto-java"

  // Configure the protoc executable
  protoc {
    // Download from repositories
    artifact = 'com.google.protobuf:protoc:3.21.1'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.46.0'
    }
  }
  generateProtoTasks {
    all()*.plugins { grpc {} }
  }
}

sourceSets {
  main {
    java {
      srcDirs 'build/generated/source/proto/main/grpc'
      srcDirs 'build/generated/source/proto/main/java'
    }
  }
}

springBoot {
  buildInfo()
}